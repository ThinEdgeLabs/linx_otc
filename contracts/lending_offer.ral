Contract LendingOffer(
  lender: Address,
  lendingTokenId: ByteVec,
  collateralTokenId: ByteVec,
  lendingAmount: U256,
  collateralAmount: U256,
  interestRate: U256,
  duration: U256,
  mut borrower: Address
) {

  event OfferTaken(borrower: Address, offerId: ByteVec)
  event OfferCancelled(offerId: ByteVec)
  event LoanLiquidated(offerId: ByteVec)
  event LoanPaidBack(offerId: ByteVec)

  enum ErrorCodes {
    OfferAlreadyTaken = 1
    BorrowerAllowedOnly = 2
    IncorrectCollateralAmount = 3
    LenderAllowedOnly = 4
    OfferNotTaken = 5
  }

  pub fn getLender() -> Address {
    return lender
  }

  pub fn getLendingTokenId() -> ByteVec {
    return lendingTokenId
  }

  pub fn getCollateralTokenId() -> ByteVec {
    return collateralTokenId
  }

  pub fn getLendingAmount() -> U256 {
    return lendingAmount
  }

  pub fn getCollateralAmount() -> U256 {
    return collateralAmount
  }

  pub fn getInterestRate() -> U256 {
    return interestRate
  }

  pub fn getDuration() -> U256 {
    return duration
  }

  pub fn getBorrower() -> Address {
    return borrower
  }

  @using(assetsInContract = true, preapprovedAssets = true, checkExternalCaller = false, updateFields = true)
  pub fn take(collateral: U256) -> () {
    assert!(lender == borrower, ErrorCodes.OfferAlreadyTaken)
    assert!(collateral == collateralAmount, ErrorCodes.IncorrectCollateralAmount)

    borrower = callerAddress!()

    transferTokenToSelf!(borrower, collateralTokenId, collateralAmount)
    transferTokenFromSelf!(borrower, lendingTokenId, lendingAmount)

    emit OfferTaken(borrower, selfContractId!())
  }

  @using(assetsInContract = true)
  pub fn cancel() -> () {
    checkCaller!(callerAddress!() == lender, ErrorCodes.LenderAllowedOnly)
    assert!(lender == borrower, ErrorCodes.OfferAlreadyTaken)

    emit OfferCancelled(selfContractId!())

    destroySelf!(lender)
  }

  @using(assetsInContract = true, preapprovedAssets = true)
  pub fn payback() -> () {
    checkCaller!(callerAddress!() == borrower, ErrorCodes.BorrowerAllowedOnly)
    transferToken!(borrower, lender, lendingTokenId, lendingAmount)
    transferTokenFromSelf!(borrower, collateralTokenId, collateralAmount)

    // TODO: Pay the interest to the lender

    emit LoanPaidBack(selfContractId!())

    destroySelf!(lender)
  }

  @using(assetsInContract = true)
  pub fn liquidate() -> () {
    checkCaller!(callerAddress!() == lender, ErrorCodes.LenderAllowedOnly)
    assert!(borrower != lender, ErrorCodes.OfferNotTaken)
    // TODO: Check if the loan is overdue

    emit LoanLiquidated(selfContractId!())

    destroySelf!(lender)
  }
}