Contract LendingOffer(
  lender: Address,
  lendingTokenId: ByteVec,
  collateralTokenId: ByteVec,
  marketplaceContractId: ByteVec,
  lendingAmount: U256,
  collateralAmount: U256,
  interestRate: U256, // in basis points
  duration: U256, // in days
  mut borrower: Address,
  mut loanTimeStamp: U256 // in seconds
) extends LendingMarketplaceUtils() {

  event OfferTaken(borrower: Address, offerId: ByteVec)
  event LoanLiquidated(offerId: ByteVec)

  enum ErrorCodes {
    MarketplaceAllowedOnly = 0
    OfferAlreadyTaken = 1
    IncorrectCollateralAmount = 3
    LenderAllowedOnly = 4
    OfferNotTaken = 5
    LoanNotOverdue = 6
  }

  pub fn getLender() -> Address {
    return lender
  }

  pub fn getLendingTokenId() -> ByteVec {
    return lendingTokenId
  }

  pub fn getCollateralTokenId() -> ByteVec {
    return collateralTokenId
  }

  pub fn getLendingAmount() -> U256 {
    return lendingAmount
  }

  pub fn getCollateralAmount() -> U256 {
    return collateralAmount
  }

  pub fn getInterestRate() -> U256 {
    return interestRate
  }

  pub fn getDuration() -> U256 {
    return duration
  }

  pub fn getBorrower() -> Address {
    return borrower
  }

  pub fn getLoanTimeStamp() -> U256 {
    return loanTimeStamp
  }

  @using(assetsInContract = true, preapprovedAssets = true, checkExternalCaller = false, updateFields = true)
  pub fn take(collateral: U256) -> () {
    assert!(lender == borrower, ErrorCodes.OfferAlreadyTaken)
    assert!(collateral == collateralAmount, ErrorCodes.IncorrectCollateralAmount)

    // TODO: Pay the fee to the marketplace

    borrower = callerAddress!()
    loanTimeStamp = blockTimeStampInSeconds()

    transferTokenToSelf!(borrower, collateralTokenId, collateralAmount)
    transferTokenFromSelf!(borrower, lendingTokenId, lendingAmount)

    emit OfferTaken(borrower, selfContractId!())
  }

  @using(assetsInContract = true)
  pub fn cancel() -> () {
    checkCaller!(callerContractId!() == marketplaceContractId, ErrorCodes.MarketplaceAllowedOnly)
    assert!(lender == borrower, ErrorCodes.OfferAlreadyTaken)
    destroySelf!(lender)
  }

  @using(assetsInContract = true, preapprovedAssets = true)
  pub fn payback() -> () {
    checkCaller!(callerContractId!() == marketplaceContractId, ErrorCodes.MarketplaceAllowedOnly)

    let interest = calculateInterestPayment(blockTimeStampInSeconds(), loanTimeStamp, lendingAmount, interestRate, duration)
    transferToken!(borrower, lender, lendingTokenId, lendingAmount + interest)
    transferTokenFromSelf!(borrower, collateralTokenId, collateralAmount)

    destroySelf!(lender)
  }

  @using(assetsInContract = true)
  pub fn liquidate() -> () {
    checkCaller!(callerAddress!() == lender, ErrorCodes.LenderAllowedOnly)
    assert!(borrower != lender, ErrorCodes.OfferNotTaken)

    let currentBlockTimeStamp = blockTimeStampInSeconds()
    assert!(currentBlockTimeStamp > loanTimeStamp + duration * Day, ErrorCodes.LoanNotOverdue)

    emit LoanLiquidated(selfContractId!())

    destroySelf!(lender)
  }
}
