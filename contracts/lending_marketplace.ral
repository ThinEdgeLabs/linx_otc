Contract LendingMarketplace(
  lendingOfferTemplateId: ByteVec,
  mut admin: Address,
  mut totalLendingOffers: U256,
  mut fee: U256,
  mut lendingEnabled: Bool
) extends LendingMarketplaceUtils() {

  event AdminUpdated(
    previous: Address,
    new: Address
  )

  event OfferCreated(
    lendingTokenId: ByteVec,
    collateralTokenId: ByteVec,
    lendingAmount: U256,
    collateralAmount: U256,
    interestRate: U256,
    duration: U256,
    lender: Address,
    lendingOfferContractId: ByteVec
  )

  event OfferCancelled(offerId: ByteVec)
  event LoanPaid(loanId: ByteVec)
  event LoanStarted(loanId: ByteVec, borrower: Address)
  event LoanLiquidated(loanId: ByteVec)

  enum ErrorCodes {
    AdminAllowedOnly = 0
    LendingDisabled = 1
    LenderAllowedOnly = 2
    BorrowerAllowedOnly = 3
    LenderNotAllowed = 4
  }

  pub fn getAdmin() -> Address {
    return admin
  }

  pub fn getTotalLendingOffers() -> U256 {
    return totalLendingOffers
  }

  pub fn getFee() -> U256 {
    return fee
  }

  @using(preapprovedAssets = true, checkExternalCaller = false, updateFields = true)
  pub fn createLendingOffer(
    lendingTokenId: ByteVec,
    collateralTokenId: ByteVec,
    lendingAmount: U256,
    collateralAmount: U256,
    interestRate: U256,
    duration: U256
  ) -> (Address) {
    assert!(lendingEnabled == true, ErrorCodes.LendingDisabled)
    let lender = callerAddress!()

    // Checking for overflow
    let _ = calculateTotalInterestPayment(lendingAmount, interestRate, duration)

    let key = toByteVec!(totalLendingOffers)
    let (encodeImmutableFields, encodeMutableFields) = LendingOffer.encodeFields!(lender, lendingTokenId, collateralTokenId, selfContractId!(), lendingAmount, collateralAmount, interestRate, duration, lender, blockTimeStampInSeconds())
    let lendingOfferContractId = copyCreateSubContract!{lender -> ALPH: 1 alph, lendingTokenId: lendingAmount}(
      key, lendingOfferTemplateId, encodeImmutableFields, encodeMutableFields
    )
    totalLendingOffers = totalLendingOffers + 1

    emit OfferCreated(lendingTokenId, collateralTokenId, lendingAmount, collateralAmount, interestRate, duration, lender, lendingOfferContractId)

    return contractIdToAddress!(lendingOfferContractId)
  }

  @using(preapprovedAssets = true)
  pub fn borrow(offerId: ByteVec) -> () {
    let offer = LendingOffer(offerId)
    let lender = offer.getLender()
    let collateralTokenId = offer.getCollateralTokenId()
    let collateralAmount = offer.getCollateralAmount()
    checkCaller!(callerAddress!() != lender, ErrorCodes.LenderNotAllowed)
    let borrower = callerAddress!()
    offer.take{borrower -> collateralTokenId: collateralAmount}(borrower)
    emit LoanStarted(offerId, borrower)
  }

  pub fn cancelOffer(offerId: ByteVec) -> () {
    let offer = LendingOffer(offerId)
    let lender = offer.getLender()
    checkCaller!(callerAddress!() == lender, ErrorCodes.LenderAllowedOnly)
    offer.cancel()
    emit OfferCancelled(offerId)
  }

  @using(preapprovedAssets = true)
  pub fn paybackLoan(loanId: ByteVec) -> () {
    let loan = LendingOffer(loanId)
    let borrower = loan.getBorrower()
    let lendingTokenId = loan.getLendingTokenId()
    let loanAmount = loan.getLendingAmount()
    let interest = loan.getInterest()
    let caller = callerAddress!()
    checkCaller!(caller == borrower, ErrorCodes.BorrowerAllowedOnly)
    loan.payback{caller -> lendingTokenId: loanAmount + interest}()
    emit LoanPaid(loanId)
  }

  pub fn liquidateLoan(loanId: ByteVec) -> () {
    let loan = LendingOffer(loanId)
    let lender = loan.getLender()
    checkCaller!(callerAddress!() == lender, ErrorCodes.LenderAllowedOnly)
    loan.liquidate()
    emit LoanLiquidated(loanId)
  }

  @using(updateFields = true)
  pub fn updateAdmin(newAdmin: Address) -> () {
    checkCaller!(callerAddress!() == admin, ErrorCodes.AdminAllowedOnly)

    emit AdminUpdated(admin, newAdmin)
    admin = newAdmin
  }

  @using(updateFields = true)
  pub fn updateFee(newFee: U256) -> () {
    checkCaller!(callerAddress!() == admin, ErrorCodes.AdminAllowedOnly)

    fee = newFee
  }

  @using(updateFields = true)
  pub fn updateLendingEnabled(enabled: Bool) -> () {
    checkCaller!(callerAddress!() == admin, ErrorCodes.AdminAllowedOnly)
    lendingEnabled = enabled
  }
}